buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    jcenter()
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "edu.stanford.nicd.raytracer"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Disable the built-in NDK task (it is deprecated).
    sourceSets.main.jni.srcDirs = []

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn customNdkBuild
    }
}

task customNdkBuild << {
    def proc = "make -C src/main/".execute()
    def buffer = new StringBuffer()
    proc.consumeProcessErrorStream(buffer)

    println proc.text
    println buffer.toString()

    if (proc.exitValue()) {
        throw new RuntimeException("Failed to compile NDK code.")
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:21.0.3'
}

